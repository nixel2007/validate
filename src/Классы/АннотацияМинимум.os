// Строка - Сообщение, выводимое при невалидном значении
//
Перем _Сообщение;

// ОбластиПримененияОграничения - По умолчанию применяется "Свойство"
//
Перем _Применение;

// Минимально допустимое значение поля (включая)
Перем _Минимум;

Функция ИмяОграничения() Экспорт
	Возврат "Минимум";
КонецФункции

// Сообщение, показываемое при срабатывании ограничения
//
// Возвращаемое значение:
//   Строка
Функция Сообщение() Экспорт
	Возврат _Сообщение;
КонецФункции

// Код ограничения
//
// Возвращаемое значение:
//   Строка
Функция Код() Экспорт
	Возврат "min";
КонецФункции

// Область применения ограничения
//
// Возвращаемое значение:
//   Строка
Функция Применение() Экспорт
	Возврат _Применение;
КонецФункции

Функция Минимум() Экспорт
	Возврат _Минимум;
КонецФункции

// Аннотация ограничения значения свойства или элементов/ключей коллекции.
//
// Проаннотированный элемент должен иметь значение не меньше, чем указанное значение.
//
// Параметры:
//   Значение    - Число  - минимально допустимое значение (включительно).
//   Сообщение   - Строка - текст сообщения (по умолчанию формируется автоматически).
//   Применение  - Строка - область применения: "Свойство" (по умолчанию), "Элемент" или "Ключ".
//
&Аннотация("Минимум")
Процедура ПриСозданииОбъекта(Значение, Сообщение = "", Применение = "Свойство")
	_Минимум = Значение;
	Если Сообщение = "" Тогда
		_Сообщение = "Значение не может быть меньше <" + Формат(_Минимум, "ЧГ=0") + ">";
	Иначе
		_Сообщение = Сообщение;
	КонецЕсли;
	_Применение = Применение;
КонецПроцедуры
