// Сообщение, выводимое при невалидном значении
//
Перем _Сообщение;
Перем _Применение;

// Минимально допустимое значение поля (включая)
Перем _Минимум;

Функция ИмяОграничения() Экспорт
	Возврат "Минимум";
КонецФункции

// Сообщение, показываемое при срабатывании ограничения
//
// Возвращаемое значение:
//   Строка
Функция Сообщение() Экспорт
	Возврат _Сообщение;
КонецФункции

// Код ограничения
//
// Возвращаемое значение:
//   Строка
Функция Код() Экспорт
	Возврат "min";
КонецФункции

// Область применения ограничения
//
// Возвращаемое значение:
//   Строка
Функция Применение() Экспорт
	Возврат _Применение;
КонецФункции

Функция Минимум() Экспорт
	Возврат _Минимум;
КонецФункции

// Аннотация ограничения значения свойства или элементов/ключей коллекции.
//
// Проаннотированный элемент должен иметь значение не меньше, чем указанное значение.
//
// Параметры:
//   Значение    - Число  - минимально допустимое значение (включительно).
//   Применение  - Строка - область применения: "Свойство" (по умолчанию), "Элемент" или "Ключ".
//
&Аннотация("Минимум")
Процедура ПриСозданииОбъекта(Значение, Применение = "Свойство")
	_Минимум = Значение;
	_Сообщение = "Значение меньше минимального <" + Формат(_Минимум, "ЧГ=0") + ">";
	_Применение = Применение;
КонецПроцедуры
