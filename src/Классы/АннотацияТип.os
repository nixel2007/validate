// Строка - Сообщение, выводимое при невалидном значении
//
Перем _Сообщение;

// Тип - Тип значения, который должно иметь проверяемое значение
//
Перем _Тип;

// ОбластиПримененияОграничения - По умолчанию применяется "Свойство"
//
Перем _Применение;

// Представление имени ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяОграничения() Экспорт
	Возврат "Тип";
КонецФункции

// Сообщение, показываемое при срабатывании ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Сообщение() Экспорт
	Возврат _Сообщение;
КонецФункции

// Код ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Код() Экспорт
	Возврат "type";
КонецФункции

// Область применения ограничения
//
// Возвращаемое значение:
//   ОбластьПримененияОграничения
//
Функция Применение() Экспорт
	Возврат _Применение;
КонецФункции

// Возвращает тип значения, который должно иметь проверяемое значение
//
// Возвращаемое значение:
//   Тип
//
Функция ТипЗначения() Экспорт
	Возврат _Тип;
КонецФункции

// Аннотация ограничения значения свойства или элементов/ключей коллекции.
//
// Проаннотированный элемент должен иметь такой же тип значения,
// что и значение, переданное в аннотацию.
//
// Параметры:
//   Значение    - Строка - тип значения, который должен иметь проверяемый элемент.
//   Применение  - Строка - область применения: "Свойство" (по умолчанию), "Элемент" или "Ключ".
//
&Аннотация("Тип")
Процедура ПриСозданииОбъекта(Значение, Применение = "Свойство")
	_Тип = Тип(Значение);
	_Сообщение = "Тип значения отличается от допустимого <" + _Тип + ">";
	_Применение = Применение;
КонецПроцедуры
