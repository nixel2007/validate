Перем Валидатор;

Перем СоответствиеВалидацийИОграничений;
Перем Рефлектор;

Функция ЭтоАннотацияВалидации(ИмяАннотации) Экспорт
	Возврат СоответствиеВалидацийИОграничений.Получить(НРег(ИмяАннотации)) <> Неопределено;
КонецФункции

Функция СоздатьОбъектВалидации(Ограничение) Экспорт

	ИмяОграничения = Ограничение.ИмяОграничения();
	ИмяКлассаВалидации = СоответствиеВалидацийИОграничений.Получить(НРег(ИмяОграничения));

	Валидация = Новый(ИмяКлассаВалидации);
	Валидация.Инициализировать(Ограничение);
	
	Если Рефлектор.МетодСуществует(Валидация, "УстановитьВалидатор") Тогда
		Валидация.УстановитьВалидатор(Валидатор);
	КонецЕсли;

	Возврат Валидация;

КонецФункции

Процедура ПриСозданииОбъекта(пВалидатор)
	Валидатор = пВалидатор;

	Рефлектор = Новый Рефлектор;

	СоответствиеВалидацийИОграничений = Новый Соответствие;
	СоответствиеВалидацийИОграничений.Вставить("валидно",   "ВалидацияОграниченияВалидно");
	СоответствиеВалидацийИОграничений.Вставить("заполнено", "ВалидацияОграниченияЗаполнено");
	СоответствиеВалидацийИОграничений.Вставить("истина",    "ВалидацияОграниченияИстина");
	СоответствиеВалидацийИОграничений.Вставить("ложь",      "ВалидацияОграниченияЛожь");
	СоответствиеВалидацийИОграничений.Вставить("максимум",  "ВалидацияОграниченияМаксимум");
	СоответствиеВалидацийИОграничений.Вставить("минимум",   "ВалидацияОграниченияМинимум");
	СоответствиеВалидацийИОграничений.Вставить("тип",       "ВалидацияОграниченияТип");

КонецПроцедуры