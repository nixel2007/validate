#Использовать reflector

Перем Рефлектор;

Перем ФабрикаВалидаций;

Процедура УстановитьФабрикуВалидаций(пФабрикаВалидаций) Экспорт
	
	ФабрикаВалидаций = пФабрикаВалидаций;
	
КонецПроцедуры

Функция Валидировать(Объект) Экспорт
	
	РезультатВалидации = Новый Массив;
	
	РефлекторОбъекта = Новый РефлекторОбъекта(Объект);
	Свойства = РефлекторОбъекта.ПолучитьТаблицуСвойств(Неопределено, Истина);

	Для Каждого Свойство Из Свойства Цикл
		
		Для Каждого Аннотация Из Свойство.Аннотации Цикл
			
			ОбъектыВалидации = ФабрикаВалидаций.ПолучитьОбъектыВалидации(Аннотация);
			Если ОбъектыВалидации = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Значение = Рефлектор.ПолучитьСвойство(Объект, Свойство.Имя);
		
			Валидация = ОбъектыВалидации.Валидация;
			ЗначениеВалидно = Валидация.ЗначениеВалидно(Значение);
			
			Если НЕ ЗначениеВалидно Тогда
				Ограничение = ОбъектыВалидации.Ограничение;
				Нарушение = Новый Нарушение(Объект, "Поле", Свойство.Имя, Ограничение.Сообщение(), Ограничение.Код());
				
				РезультатВалидации.Добавить(Нарушение);
			КонецЕсли;

		КонецЦикла;
	
	КонецЦикла;
	
	Возврат РезультатВалидации;
	
КонецФункции

Процедура ПриСозданииОбъекта()

	Рефлектор = Новый Рефлектор;
	ФабрикаВалидаций = Новый СтандартнаяФабрикаВалидаций(ЭтотОбъект);

КонецПроцедуры