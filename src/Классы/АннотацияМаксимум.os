// Строка - Сообщение, выводимое при невалидном значении
//
Перем _Сообщение;

// ОбластиПримененияОграничения - По умолчанию применяется "Свойство"
//
Перем _Применение;

// Максимально допустимое значение поля (включая).
Перем _Максимум;

// Представление имени ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяОграничения() Экспорт
	Возврат "Максимум";
КонецФункции

// Сообщение, показываемое при срабатывании ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Сообщение() Экспорт
	Возврат _Сообщение;
КонецФункции

// Код ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Код() Экспорт
	Возврат "max";
КонецФункции

// Область применения ограничения
//
// Возвращаемое значение:
//   ОбластьПримененияОграничения
//
Функция Применение() Экспорт
	Возврат _Применение;
КонецФункции

// Максимально допустимое значение поля (включая).
//
// Возвращаемое значение:
//   Число
//
Функция Максимум() Экспорт
	Возврат _Максимум;
КонецФункции

// Аннотация ограничения значения свойства или элементов/ключей коллекции.
//
// Проаннотированный элемент должен иметь значение не больше, чем указанное значение.
//
// Параметры:
//   Значение    - Число  - максимально допустимое значение (включительно).
//   Сообщение   - Строка - текст сообщения (по умолчанию формируется автоматически).
//   Применение  - Строка - область применения: "Свойство" (по умолчанию), "Элемент" или "Ключ".
//
&Аннотация("Максимум")
Процедура ПриСозданииОбъекта(Значение, Сообщение = "", Применение = "Свойство") Экспорт
	_Максимум = Значение;
	Если Сообщение = "" Тогда
		_Сообщение = "Значение больше максимального <" + Формат(_Максимум, "ЧГ=0") + ">";
	Иначе
		_Сообщение = Сообщение;
	КонецЕсли;
	_Применение = Применение;
КонецПроцедуры
