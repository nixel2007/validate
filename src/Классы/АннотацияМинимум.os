// Строка - Сообщение, выводимое при невалидном значении
//
Перем _Сообщение;

// Минимально допустимое значение поля (включая)
Перем _Минимум;

// Представление имени ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяОграничения() Экспорт
	Возврат "Минимум";
КонецФункции

// Сообщение, показываемое при срабатывании ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Сообщение() Экспорт
	Возврат _Сообщение;
КонецФункции

// Код ограничения
//
// Возвращаемое значение:
//   Строка
//
Функция Код() Экспорт
	Возврат "min";
КонецФункции

// Минимально допустимое значение поля (включая).
//
// Возвращаемое значение:
//   Число
//
Функция Минимум() Экспорт
	Возврат _Минимум;
КонецФункции

// Аннотация ограничения значения свойства или элементов/ключей коллекции.
//
// Проаннотированный элемент должен иметь значение не меньше, чем указанное значение.
//
// Параметры:
//   Значение    - Число  - минимально допустимое значение (включительно).
//   Сообщение   - Строка - текст сообщения (по умолчанию формируется автоматически).
//
&Аннотация("Минимум")
Процедура ПриСозданииОбъекта(Значение, Сообщение = "")
	_Минимум = Значение;
	Если Сообщение = "" Тогда
		_Сообщение = "Значение меньше минимального <" + Формат(_Минимум, "ЧГ=0") + ">";
	Иначе
		_Сообщение = Сообщение;
	КонецЕсли;
КонецПроцедуры
