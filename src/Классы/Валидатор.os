#Использовать annotations
#Использовать reflector

Перем КонтейнерАннотаций;
Перем ФабрикаВалидаций;

Перем Рефлектор;
Перем КэшВалидаций;

Функция Валидировать(Объект) Экспорт
	
	РезультатВалидации = Новый Массив;
	
	РефлекторОбъекта = Новый РефлекторОбъекта(Объект);
	Свойства = РефлекторОбъекта.ПолучитьТаблицуСвойств(Неопределено, Истина);

	Для Каждого Свойство Из Свойства Цикл
		
		Для Каждого Аннотация Из Свойство.Аннотации Цикл
			
			ОпределениеАннотации = КонтейнерАннотаций.ПолучитьОпределениеАннотации(Аннотация.Имя);

			Если ОпределениеАннотации = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
		
			Ограничение = ОпределениеАннотации.СоздатьОбъектАннотации(Аннотация);
			Валидация = ФабрикаВалидаций.СоздатьОбъектВалидации(Ограничение);
						
			Значение = Рефлектор.ПолучитьСвойство(Объект, Свойство.Имя);
		
			ЗначениеВалидно = Валидация.ЗначениеВалидно(Значение);
			
			Если НЕ ЗначениеВалидно Тогда
				Нарушение = Новый Нарушение(Объект, "Поле", Свойство.Имя, Ограничение.Сообщение(), Ограничение.Код());
				
				РезультатВалидации.Добавить(Нарушение);
			КонецЕсли;

		КонецЦикла;
	
	КонецЦикла;
	
	Возврат РезультатВалидации;
	
КонецФункции

Процедура ПриСозданииОбъекта(пКонтейнерАнноаций = Неопределено)

	Рефлектор = Новый Рефлектор;
	
	Если пКонтейнерАнноаций = Неопределено Тогда
		КонтейнерАннотаций = Новый КонтейнерАннотаций;
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияОграничение"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияВалидно"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияЗаполнено"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияИстина"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияЛожь"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияМаксимум"));
		КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияМинимум"));
	Иначе
		КонтейнерАннотаций = пКонтейнерАнноаций;
	КонецЕсли;

	ФабрикаВалидаций = Новый СтандартнаяФабрикаВалидаций(ЭтотОбъект);
	КэшВалидаций = Новый Соответствие();

КонецПроцедуры
